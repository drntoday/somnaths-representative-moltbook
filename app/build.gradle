plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'com.somnath.representative'
    compileSdk 34

    defaultConfig {
        applicationId 'com.somnath.representative'
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName '1.0'
    }

    def signingStoreFile = System.getenv('SIGNING_STORE_FILE')
    def signingStorePassword = System.getenv('SIGNING_STORE_PASSWORD')
    def signingKeyAlias = System.getenv('SIGNING_KEY_ALIAS')
    def signingKeyPassword = System.getenv('SIGNING_KEY_PASSWORD')
    def releaseSigningMissing = [
            'SIGNING_STORE_FILE': signingStoreFile,
            'SIGNING_STORE_PASSWORD': signingStorePassword,
            'SIGNING_KEY_ALIAS': signingKeyAlias,
            'SIGNING_KEY_PASSWORD': signingKeyPassword
    ].findAll { key, value -> value == null || value.trim().isEmpty() }.keySet()

    signingConfigs {
        release {
            if (releaseSigningMissing.isEmpty()) {
                storeFile file(signingStoreFile)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (releaseSigningMissing.isEmpty()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
        buildConfig true
    }
}


configurations.all {
    resolutionStrategy.force(
            'androidx.core:core:1.13.1',
            'androidx.core:core-ktx:1.13.1',
            'androidx.activity:activity:1.9.3',
            'androidx.activity:activity-ktx:1.9.3',
            'androidx.activity:activity-compose:1.9.3',
            'androidx.work:work-runtime:2.9.0',
            'androidx.work:work-runtime-ktx:2.9.0'
    )
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation platform('androidx.compose:compose-bom:2025.01.01')

    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    implementation 'androidx.compose.material3:material3'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.navigation:navigation-compose:2.8.5'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    debugImplementation 'androidx.compose.ui:ui-tooling'
}
